start : cmd+

?cmd : define
     | imp
     | macro

?imp : "import" "{" args "}" "from" name

macro : "macro" name "(" args? ")" "{" expr "}"
args : name ("," name)*

define : name "=" expr ";"

html : name children?       -> html_simple
     | name atrib children? -> html_full

atrib : "(" param ("," param)* ")"
param : name "=" expr

?expr : atom
      | subs 
      | html
      | param

?subs : "$" NAME ("." NAME)*
    
?atom : STRING -> string
      | INT -> int
      | FLOAT -> float
      | "(" expr ")"

name  : NAME

children : expr
         | "{" expr+ "}"


// Terminais
SYMBOL.0  : /[-!+\/*@$%^&~<>?|\\\w=]+/
STRING    : /"[^"\\]*(\\[^\n\t\r\f][^"\\]*)*"/
INT.1     : /-?\d+/
FLOAT.2   : /-?\d+\.\d+/
NAME: /[a-zA-Z]\w*/

%ignore /\s+/
%ignore /#[^\n]*/